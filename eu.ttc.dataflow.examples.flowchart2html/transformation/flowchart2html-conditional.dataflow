// This shows that it might be interesting to be able to
// reuse subflows - how would it work in the language?

AllInstances allActions {
	field element
	type Flowchart!Action
	target markAsAction
}

Evaluate markAsAction {
	field elementType
	expression 'A'
	target containerDiv
}

AllInstances allDecisions {
	field element
	type Flowchart!Decision
	target markAsDecision
}

Evaluate markAsDecision {
	field elementType
	expression 'D'
	target containerDiv
}

NewInstance containerDiv {
	field container
	key element
	type HTML!DIV
	target titleDiv
}

NewInstance titleDiv {
	field title
	key element
	type HTML!H1
	target titleValue
}

SetFeature titleValue {
	field title
	value element.name
	feature ^value
	target containerChildren
}

Evaluate containerChildren {
	field children
	expression container.children
	target addTitle
}

AddToContainer addTitle {
	field children
	value title
	target outgoing
}

Evaluate outgoing {
	field lOutgoing
	expression element.outgoing
	target forEachOutgoing
}

ForEach forEachOutgoing {
	listField lOutgoing
	itemField iOutgoing
	positionField pOutgoing
	target filterOutgoing
}

Filter filterOutgoing {
	// We want all the outgoing links of the decisions,
	// and only the first link of the actions
	filterBy elementType = 'D' or pOutgoing = 0
	target outgoingLink
}

NewInstance outgoingLink {
	field outgoingLink
	// Need to produce combined key - future version
	// might want to support composite keys
	key element + iOutgoing
	type HTML!A
	target addOutgoingLink
}

AddToContainer addOutgoingLink {
	field children
	value outgoingLink
	target outgoingLinkHref
}

SetFeature outgoingLinkHref {
	field outgoingLink
	value iOutgoing.^target.name
	feature ahref
	target outgoingLinkValue
}

SetFeature outgoingLinkValue {
	field outgoingLink
	value if elementType = 'A' then 'Next steps' else iOutgoing.^target.name
	feature ^value
}
